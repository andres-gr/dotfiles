import QtQuick
import QtQuick.Layouts
import SddmComponents 2.0 as Sddm
import QtQuick.Controls

import "./Widgets/"
import "./Commons/"

FocusScope {
    id: root
    width: 1920
    height: 1080

    // --- LOGIC VARIABLES ---
    property int currentSessionIndex: 0
    property bool usersReady: false

    Component.onCompleted: {
        currentSessionIndex = sessionModel.lastIndex;
    }

    property bool uiEnabled: true
    property string loginErrorMessage: ""
    property string currentTime: Qt.formatDateTime(new Date(), "hh:mm")
    property string currentDate: Qt.formatDateTime(new Date(), "dddd, MMMM d")

    property bool loggingIn: false

    property string currentUserName: userModel.lastUser
    property var activeUser: null
    property var userMap: ({})
    property var userListModel: []

    function getUser(username) {
        if (root.userMap && root.userMap[username]) {
            return root.userMap[username];
        }
        return null;
    }

    // Use Instantiator to populate userMap synchronously
    Instantiator {
        id: userInstantiator
        model: userModel

        delegate: QtObject {
            readonly property string userName: model.name
            readonly property string userRealName: model.realName
            readonly property string userIcon: model.icon
            readonly property int userIndex: index

            Component.onCompleted: {
                console.log("Loading user:", userName, userRealName, userIcon);

                var userData = {
                    "name": userName,
                    "realName": userRealName,
                    "icon": userIcon
                };

                // Add to map without reassigning the property
                root.userMap[userName] = userData;

                // Check if we're done loading all users
                var loadedCount = Object.keys(root.userMap).length;
                console.log("Loaded", loadedCount, "of", userModel.count);

                if (loadedCount === userModel.count) {
                    // All users loaded, now set active user
                    if (root.currentUserName && root.userMap[root.currentUserName]) {
                        console.log("Setting active user to last user:", root.currentUserName);
                        root.activeUser = root.userMap[root.currentUserName];
                    } else {
                        var firstKey = Object.keys(root.userMap)[0];
                        if (firstKey) {
                            console.log("Setting active user to first user:", firstKey);
                            root.currentUserName = firstKey;
                            root.activeUser = root.userMap[firstKey];
                        }
                    }
                    // Build flat JS array for user combo
                    var list = [];
                    var keys = Object.keys(root.userMap);
                    for (var k = 0; k < keys.length; k++) {
                        var u = root.userMap[keys[k]];
                        list.push({
                            name: u.name,
                            displayName: (u.realName && u.realName !== "") ? u.realName : u.name
                        });
                    }
                    root.userListModel = list;
                    root.usersReady = true;
                }
            }
        }
    }

    function startLogin(password) {
        if (password == "") {
            console.log("Error: Password is empty");
            return;
        }

        if (!root.activeUser) {
            console.log("ERROR: activeUser is undefined! currentUserName:", root.currentUserName);

            root.loggingIn = false;
            root.loginErrorMessage = "Invalid username";
            root.uiEnabled = true;

            passwordComponent.clear();
            passwordComponent.forceFocus();
            return;
        }

        root.loginErrorMessage = "";
        root.uiEnabled = false;
        root.loggingIn = true;

        sddm.login(root.currentUserName, password, root.currentSessionIndex);
    }

    // --- LOAD FONT ---
    FontLoader {
        id: iconFontLoader
        source: "Assets/Fonts/tabler/tabler-icons.ttf"
    }
    property string iconFont: iconFontLoader.name

    // ---------------------------------------------------------
    // 1. BACKGROUND
    // ---------------------------------------------------------
    Image {
        id: lockBgImage
        anchors.fill: parent
        fillMode: Image.PreserveAspectCrop
        source: config.background || "assets/background.jpg"
        cache: true
        smooth: true
        mipmap: false
        antialiasing: true
    }

    Rectangle {
        anchors.fill: parent
        visible: config.hideShadow !== "true"
        gradient: Gradient {
            GradientStop {
                position: 0.0
                color: Qt.alpha(Color.mShadow, 0.8)
            }
            GradientStop {
                position: 0.3
                color: Qt.alpha(Color.mShadow, 0.4)
            }
            GradientStop {
                position: 0.7
                color: Qt.alpha(Color.mShadow, 0.5)
            }
            GradientStop {
                position: 1.0
                color: Qt.alpha(Color.mShadow, 0.9)
            }
        }
    }

    // Screen corners for lock screen
    Item {
        anchors.fill: parent
        visible: true

        property color cornerColor: Color.black
        property real cornerRadius: Style.screenRadius
        property real cornerSize: Style.screenRadius

        // Top-left concave corner
        Canvas {
            anchors.top: parent.top
            anchors.left: parent.left
            width: parent.cornerSize
            height: parent.cornerSize
            antialiasing: true
            renderTarget: Canvas.FramebufferObject
            smooth: false

            onPaint: {
                const ctx = getContext("2d");
                if (!ctx)
                    return;
                ctx.reset();
                ctx.clearRect(0, 0, width, height);

                ctx.fillStyle = parent.cornerColor;
                ctx.fillRect(0, 0, width, height);

                ctx.globalCompositeOperation = "destination-out";
                ctx.fillStyle = "#ffffff";
                ctx.beginPath();
                ctx.arc(width, height, parent.cornerRadius, 0, 2 * Math.PI);
                ctx.fill();
            }

            onWidthChanged: if (available)
                requestPaint()
            onHeightChanged: if (available)
                requestPaint()
        }

        // Top-right concave corner
        Canvas {
            anchors.top: parent.top
            anchors.right: parent.right
            width: parent.cornerSize
            height: parent.cornerSize
            antialiasing: true
            renderTarget: Canvas.FramebufferObject
            smooth: true

            onPaint: {
                const ctx = getContext("2d");
                if (!ctx)
                    return;
                ctx.reset();
                ctx.clearRect(0, 0, width, height);

                ctx.fillStyle = parent.cornerColor;
                ctx.fillRect(0, 0, width, height);

                ctx.globalCompositeOperation = "destination-out";
                ctx.fillStyle = "#ffffff";
                ctx.beginPath();
                ctx.arc(0, height, parent.cornerRadius, 0, 2 * Math.PI);
                ctx.fill();
            }

            onWidthChanged: if (available)
                requestPaint()
            onHeightChanged: if (available)
                requestPaint()
        }

        // Bottom-left concave corner
        Canvas {
            anchors.bottom: parent.bottom
            anchors.left: parent.left
            width: parent.cornerSize
            height: parent.cornerSize
            antialiasing: true
            renderTarget: Canvas.FramebufferObject
            smooth: true

            onPaint: {
                const ctx = getContext("2d");
                if (!ctx)
                    return;
                ctx.reset();
                ctx.clearRect(0, 0, width, height);

                ctx.fillStyle = parent.cornerColor;
                ctx.fillRect(0, 0, width, height);

                ctx.globalCompositeOperation = "destination-out";
                ctx.fillStyle = "#ffffff";
                ctx.beginPath();
                ctx.arc(width, 0, parent.cornerRadius, 0, 2 * Math.PI);
                ctx.fill();
            }

            onWidthChanged: if (available)
                requestPaint()
            onHeightChanged: if (available)
                requestPaint()
        }

        // Bottom-right concave corner
        Canvas {
            anchors.bottom: parent.bottom
            anchors.right: parent.right
            width: parent.cornerSize
            height: parent.cornerSize
            antialiasing: true
            renderTarget: Canvas.FramebufferObject
            smooth: true

            onPaint: {
                const ctx = getContext("2d");
                if (!ctx)
                    return;
                ctx.reset();
                ctx.clearRect(0, 0, width, height);

                ctx.fillStyle = parent.cornerColor;
                ctx.fillRect(0, 0, width, height);

                ctx.globalCompositeOperation = "destination-out";
                ctx.fillStyle = "#ffffff";
                ctx.beginPath();
                ctx.arc(0, 0, parent.cornerRadius, 0, 2 * Math.PI);
                ctx.fill();
            }

            onWidthChanged: if (available)
                requestPaint()
            onHeightChanged: if (available)
                requestPaint()
        }
    }

    // ---------------------------------------------------------
    // 2. TOP RECTANGLE (Clock & User)
    // ---------------------------------------------------------
    Item {
        anchors.fill: parent
        Rectangle {
            width: Math.max(500, contentRow.implicitWidth + 32)
            height: Math.max(120, contentRow.implicitHeight + 32)
            anchors.horizontalCenter: parent.horizontalCenter
            anchors.top: parent.top
            anchors.topMargin: 100
            radius: Style.radiusL
            color: Color.mSurface
            border.color: Qt.alpha(Color.mOutline, 0.2)
            border.width: 1

            RowLayout {
                id: contentRow
                anchors.centerIn: parent
                anchors.margins: 16
                spacing: 32

                NAvatar {
                    imageSource: {
                        if (!root.usersReady || !root.activeUser) {
                            return config.DefaultAvatar || "";
                        }

                        var path = root.activeUser.icon || config.DefaultAvatar || "";
                        if (path.length > 0 && path.indexOf("/") === 0) {
                            return "file://" + path;
                        }
                        return path;
                    }
                }

                ColumnLayout {
                    // User Name
                    NText {
                        text: {
                            if (!root.usersReady || !root.activeUser) {
                                return "Invalid user";
                            }

                            var name = root.activeUser.realName || root.activeUser.name || "";
                            return (name !== "") ? "Welcome, " + name : "Invalid user";
                        }
                        pointSize: Style.fontSizeXXL
                        font.weight: Style.fontWeightMedium
                        color: Color.mOnSurface
                        horizontalAlignment: Text.AlignLeft
                    }

                    // Date
                    NText {
                        text: root.currentDate
                        pointSize: Style.fontSizeXL
                        font.weight: Style.fontWeightMedium
                        color: Color.mOnSurfaceVariant
                        horizontalAlignment: Text.AlignLeft
                    }
                }
                // Spacer
                Item {
                    width: 20
                    height: 1
                }
                NClock {
                    clockStyle: config.clockStyle
                    Layout.preferredWidth: 70
                    Layout.preferredHeight: 70
                    Layout.alignment: Qt.AlignVCenter
                    backgroundColor: Color.mSurface
                    clockColor: Color.mOnSurface
                    secondHandColor: Color.mPrimary
                    hoursFontSize: Style.fontSizeL
                    minutesFontSize: Style.fontSizeL
                }
            }
        }

        // Error notification
        Rectangle {

            visible: root.loginErrorMessage !== ""
            opacity: visible ? 1.0 : 0.0

            width: errorRowLayout.implicitWidth + Style.marginXL * 1.5
            height: 50
            anchors.horizontalCenter: parent.horizontalCenter
            anchors.bottom: parent.bottom
            anchors.bottomMargin: 320 * Style.uiScaleRatio
            radius: Style.radiusL
            color: Color.mError
            border.color: Color.mError
            border.width: 1

            RowLayout {
                id: errorRowLayout
                anchors.centerIn: parent
                spacing: 10

                NIcon {
                    icon: "alert-circle"
                    pointSize: Style.fontSizeL
                    color: Color.mOnError
                }

                NText {
                    text: root.loginErrorMessage || "Authentication failed"
                    color: Color.mOnError
                    pointSize: Style.fontSizeL
                    font.weight: Style.fontWeightMedium
                    horizontalAlignment: Text.AlignHCenter
                }
            }

            Behavior on opacity {
                NumberAnimation {
                    duration: 300
                    easing.type: Easing.OutCubic
                }
            }
        }
        // ---------------------------------------------------------
        // 2.5 STATUS BAR (Keyboard)
        // ---------------------------------------------------------
        Rectangle {
            id: statusBar

            property bool hasKeyboard: keyboard.layouts.length > 1
            visible: hasKeyboard

            anchors.horizontalCenter: bottomContainer.horizontalCenter
            anchors.bottom: bottomContainer.top
            anchors.bottomMargin: -Style.radiusL
            z: -1

            height: 30 + Style.radiusL
            width: (hasKeyboard) ? 120 * Style.uiScaleRatio : 0

            radius: Style.radiusL
            color: Color.mSurface

            RowLayout {
                anchors.top: parent.top
                anchors.topMargin: 10
                anchors.horizontalCenter: parent.horizontalCenter
                spacing: 16

                Item {
                    visible: statusBar.hasKeyboard

                    Layout.preferredWidth: kbContent.implicitWidth
                    Layout.preferredHeight: kbContent.implicitHeight

                    MouseArea {
                        anchors.fill: parent
                        cursorShape: Qt.PointingHandCursor
                        onClicked: {
                            var nextIndex = keyboard.currentLayout + 1;
                            if (nextIndex >= keyboard.layouts.length) {
                                nextIndex = 0;
                            }
                            console.log("currentLayout kb:", nextIndex);
                            keyboard.currentLayout = nextIndex;
                        }
                    }
                    RowLayout {
                        id: kbContent
                        spacing: 6
                        visible: statusBar.hasKeyboard

                        NIcon {
                            icon: "keyboard"
                            pointSize: Style.fontSizeM
                            color: Color.mOnSurfaceVariant
                        }

                        NText {
                            text: keyboard.layouts[keyboard.currentLayout] || "en"

                            color: Color.mOnSurfaceVariant
                            pointSize: Style.fontSizeM
                            font.weight: Style.fontWeightMedium
                            elide: Text.ElideRight
                        }
                    }
                }
            }
        }

        // ---------------------------------------------------------
        // 3. BOTTOM RECTANGLE (Input & Login)
        // ---------------------------------------------------------
        Rectangle {
            id: bottomContainer
            height: 180

            anchors.horizontalCenter: parent.horizontalCenter
            anchors.bottom: parent.bottom
            anchors.bottomMargin: 100
            radius: Style.radiusL
            color: Color.mSurface

            Component.onCompleted: passwordComponent.forceFocus()

            Item {
                id: buttonRowTextMeasurer
                visible: false
                property real iconSize: Style.fontSizeM
                property real fontSize: Style.fontSizeS
                property real spacing: 6
                property real padding: 18

                Text {
                    id: logoutText
                    text: "Logout"
                    font.pointSize: parent.fontSize
                    font.weight: Font.Medium
                }
                Text {
                    id: suspendText
                    text: "Suspend"
                    font.pointSize: parent.fontSize
                    font.weight: Font.Medium
                }
                Text {
                    id: rebootText
                    text: "Reboot"
                    font.pointSize: parent.fontSize
                    font.weight: Font.Medium
                }
                Text {
                    id: shutdownText
                    text: "Shutdown"
                    font.pointSize: parent.fontSize
                    font.weight: Font.Medium
                }

                property real maxTextWidth: Math.max(logoutText.implicitWidth, Math.max(suspendText.implicitWidth, Math.max(rebootText.implicitWidth, shutdownText.implicitWidth)))
                property real minButtonWidth: maxTextWidth + iconSize + spacing + padding
            }

            property int buttonCount: 4
            property int spacingCount: buttonCount - 1
            property real minButtonRowWidth: buttonRowTextMeasurer.minButtonWidth > 0 ? (buttonCount * buttonRowTextMeasurer.minButtonWidth) + (spacingCount * 10) + 40 + (2 * Style.marginM) + 28 + (2 * Style.marginM) : 750
            width: Math.max(750, minButtonRowWidth)

            ColumnLayout {
                anchors.centerIn: parent
                anchors.fill: parent
                anchors.margins: 14
                spacing: 14

                // -------------------------------------------------
                // USER DROPDOWN
                // -------------------------------------------------
                RowLayout {
                    id: userComponent
                    Layout.fillWidth: true
                    spacing: 0

                    Item {
                        Layout.preferredWidth: Style.marginM
                    }

                    NComboBox {
                        id: userComboBox
                        Layout.fillWidth: true
                        preferredHeight: 48
                        model: root.userListModel
                        textRole: "displayName"
                        showIcon: true
                        iconName: "user"
                        borderColor: Qt.alpha(Color.mOutline, 0.3)
                        borderFocusColor: Color.mPrimary
                        currentIndex: {
                            for (var i = 0; i < root.userListModel.length; i++) {
                                if (root.userListModel[i].name === root.currentUserName)
                                    return i;
                            }
                            return 0;
                        }
                        onActivated: function(index) {
                            var entry = root.userListModel[index];
                            if (!entry) return;
                            root.currentUserName = entry.name;
                            root.activeUser = root.getUser(entry.name);
                            root.loginErrorMessage = "";
                            passwordComponent.forceFocus();
                        }
                        onPopupClosed: passwordComponent.forceFocus()
                    }

                    Item {
                        Layout.preferredWidth: Style.marginM
                    }
                }
                // -------------------------------------------------
                // PASSWORD INPUT
                // -------------------------------------------------
                RowLayout {
                    id: passwordComponent
                    Layout.fillWidth: true
                    spacing: 0

                    function forceFocus() {
                        passwordInput.forceActiveFocus();
                    }
                    function clear() {
                        passwordInput.text = "";
                    }

                    Item {
                        Layout.preferredWidth: Style.marginM
                    }

                    Rectangle {
                        id: inputBackground
                        Layout.fillWidth: true
                        Layout.preferredHeight: 48
                        radius: Style.iRadiusL
                        color: Color.mSurface
                        border.color: passwordInput.activeFocus ? Color.mPrimary : Qt.alpha(Color.mOutline, 0.3)
                        border.width: passwordInput.activeFocus ? 2 : 1

                        property bool passwordVisible: false

                        MouseArea {
                            anchors.fill: parent
                            cursorShape: Qt.IBeamCursor
                            onClicked: passwordInput.forceActiveFocus()
                        }

                        Row {
                            anchors.left: parent.left
                            anchors.leftMargin: 18
                            anchors.verticalCenter: parent.verticalCenter
                            spacing: 14

                            NIcon {
                                icon: "lock"
                                pointSize: Style.fontSizeL
                                color: passwordInput.activeFocus ? Color.mPrimary : Color.mOnSurfaceVariant
                                anchors.verticalCenter: parent.verticalCenter
                            }

                            TextInput {
                                id: passwordInput
                                width: 0
                                height: 0
                                visible: true
                                enabled: root.uiEnabled
                                font.pointSize: Style.fontSizeM
                                color: "transparent"
                                cursorDelegate: Item {}
                                echoMode: inputBackground.passwordVisible ? TextInput.Normal : TextInput.Password
                                passwordCharacter: "•"
                                passwordMaskDelay: 0

                                onTextEdited: {
                                    root.loginErrorMessage = "";
                                }

                                Keys.onPressed: function (event) {
                                    if (event.key === Qt.Key_Return || event.key === Qt.Key_Enter) {
                                        root.startLogin(text);
                                    }
                                }
                            }

                            Row {
                                spacing: 0

                                Rectangle {
                                    id: cursorStart
                                    width: 2
                                    height: 20
                                    color: Color.mPrimary
                                    visible: passwordInput.activeFocus && passwordInput.text.length === 0
                                    anchors.verticalCenter: parent.verticalCenter
                                    SequentialAnimation on opacity {
                                        loops: Animation.Infinite
                                        running: cursorStart.visible
                                        NumberAnimation {
                                            to: 0
                                            duration: 530
                                        }
                                        NumberAnimation {
                                            to: 1
                                            duration: 530
                                        }
                                    }
                                }

                                Item {
                                    width: Math.min(passwordDisplayContent.width, 550)
                                    height: 20
                                    visible: passwordInput.text.length > 0 && !inputBackground.passwordVisible
                                    anchors.verticalCenter: parent.verticalCenter
                                    clip: true
                                    Row {
                                        id: passwordDisplayContent
                                        spacing: 6
                                        anchors.verticalCenter: parent.verticalCenter
                                        Repeater {
                                            model: passwordInput.text.length
                                            NIcon {
                                                icon: "circle-filled"
                                                pointSize: Style.fontSizeS
                                                color: Color.mPrimary
                                                opacity: 1.0
                                            }
                                        }
                                    }
                                }

                                NText {
                                    text: passwordInput.text
                                    color: Color.mPrimary
                                    pointSize: Style.fontSizeM
                                    font.weight: Font.Medium
                                    visible: passwordInput.text.length > 0 && inputBackground.passwordVisible
                                    anchors.verticalCenter: parent.verticalCenter
                                    elide: Text.ElideRight
                                    width: Math.min(implicitWidth, 550)
                                }

                                Rectangle {
                                    id: cursorEnd
                                    width: 2
                                    height: 20
                                    color: Color.mPrimary
                                    visible: passwordInput.activeFocus && passwordInput.text.length > 0
                                    anchors.verticalCenter: parent.verticalCenter
                                    SequentialAnimation on opacity {
                                        loops: Animation.Infinite
                                        running: cursorEnd.visible
                                        NumberAnimation {
                                            to: 0
                                            duration: 530
                                        }
                                        NumberAnimation {
                                            to: 1
                                            duration: 530
                                        }
                                    }
                                }
                            }
                        }

                        Rectangle {
                            anchors.right: submitButton.left
                            anchors.rightMargin: 4
                            anchors.verticalCenter: parent.verticalCenter
                            width: 36
                            height: 36
                            radius: Style.iRadiusL
                            color: eyeButtonArea.containsMouse ? Color.mPrimary : Color.transparent
                            visible: passwordInput.text.length > 0
                            enabled: root.uiEnabled

                            NIcon {
                                anchors.centerIn: parent
                                icon: inputBackground.passwordVisible ? "eye-off" : "eye"
                                pointSize: Style.fontSizeM
                                color: eyeButtonArea.containsMouse ? Color.mOnPrimary : Color.mOnSurfaceVariant
                                Behavior on color {
                                    ColorAnimation {
                                        duration: 200
                                    }
                                }
                            }

                            MouseArea {
                                id: eyeButtonArea
                                anchors.fill: parent
                                hoverEnabled: true
                                cursorShape: Qt.PointingHandCursor
                                onClicked: inputBackground.passwordVisible = !inputBackground.passwordVisible
                            }
                            Behavior on color {
                                ColorAnimation {
                                    duration: 200
                                }
                            }
                        }

                        Rectangle {
                            id: submitButton
                            anchors.right: parent.right
                            anchors.rightMargin: 8
                            anchors.verticalCenter: parent.verticalCenter
                            width: 36
                            height: 36
                            radius: Style.iRadiusL
                            color: submitButtonArea.containsMouse ? Color.mPrimary : Color.transparent
                            border.color: Color.mPrimary
                            border.width: Style.borderS
                            enabled: root.uiEnabled

                            NIcon {
                                anchors.centerIn: parent
                                icon: "arrow-forward"
                                pointSize: Style.fontSizeM
                                color: submitButtonArea.containsMouse ? Color.mOnPrimary : Color.mPrimary
                                Behavior on color {
                                    ColorAnimation {
                                        duration: 200
                                    }
                                }
                            }

                            MouseArea {
                                id: submitButtonArea
                                anchors.fill: parent
                                hoverEnabled: true
                                cursorShape: Qt.PointingHandCursor
                                onClicked: root.startLogin(passwordInput.text)
                            }
                            Behavior on color {
                                ColorAnimation {
                                    duration: 200
                                }
                            }
                        }

                        Behavior on border.color {
                            ColorAnimation {
                                duration: 200
                                easing.type: Easing.OutCubic
                            }
                        }
                    }

                    Item {
                        Layout.preferredWidth: Style.marginM
                    }
                }

                // -------------------------------------------------
                // SESSION & POWER BUTTONS
                // -------------------------------------------------
                RowLayout {
                    Layout.fillWidth: true
                    Layout.preferredHeight: 36
                    spacing: 0

                    Item {
                        Layout.preferredWidth: Style.marginM
                    }

                    NComboBox {
                        Layout.fillWidth: true
                        model: sessionModel
                        currentIndex: root.currentSessionIndex
                        onActivated: index => {
                            root.currentSessionIndex = index;
                            console.log("Session switched to index:", root.currentSessionIndex);
                            passwordComponent.forceFocus();
                        }
                        onPopupClosed: passwordComponent.forceFocus()
                    }

                    Item {
                        Layout.preferredWidth: 10
                    }

                    NButton {
                        Layout.fillWidth: true
                        Layout.preferredHeight: 36
                        icon: "suspend"
                        text: "Suspend"
                        outlined: true
                        backgroundColor: Color.mOnSurfaceVariant
                        textColor: Color.mOnPrimary
                        hoverColor: Color.mPrimary
                        fontSize: Style.fontSizeS
                        iconSize: Style.fontSizeM
                        fontWeight: Style.fontWeightMedium
                        horizontalAlignment: Qt.AlignHCenter
                        buttonRadius: Style.iRadiusL
                        onClicked: sddm.suspend()
                    }

                    Item {
                        Layout.preferredWidth: 10
                    }

                    NButton {
                        Layout.fillWidth: true
                        Layout.preferredHeight: 36
                        icon: "reboot"
                        text: "Reboot"
                        outlined: true
                        backgroundColor: Color.mOnSurfaceVariant
                        textColor: Color.mOnPrimary
                        hoverColor: Color.mPrimary
                        fontSize: Style.fontSizeS
                        iconSize: Style.fontSizeM
                        fontWeight: Style.fontWeightMedium
                        horizontalAlignment: Qt.AlignHCenter
                        buttonRadius: Style.iRadiusL
                        onClicked: sddm.reboot()
                    }

                    Item {
                        Layout.preferredWidth: 10
                    }

                    NButton {
                        Layout.fillWidth: true
                        Layout.preferredHeight: 36
                        icon: "shutdown"
                        text: "Shutdown"
                        outlined: true
                        backgroundColor: Color.mError
                        textColor: Color.mOnError
                        hoverColor: Color.mError
                        fontSize: Style.fontSizeS
                        iconSize: Style.fontSizeM
                        fontWeight: Style.fontWeightMedium
                        horizontalAlignment: Qt.AlignHCenter
                        buttonRadius: Style.iRadiusL
                        onClicked: sddm.powerOff()
                    }

                    Item {
                        Layout.preferredWidth: Style.marginM
                    }
                }
            }
        }
    }

    // ---------------------------------------------------------
    // 4. SIGNALS (Error Handling)
    // ---------------------------------------------------------
    Connections {
        target: sddm
        function onLoginSucceeded() {
            console.log("Login Successful.");
        }
        function onLoginFailed() {
            root.loggingIn = false;

            root.loginErrorMessage = "Authentification failed";
            root.uiEnabled = true;

            passwordComponent.clear();
            passwordComponent.forceFocus();
        }
    }
}
