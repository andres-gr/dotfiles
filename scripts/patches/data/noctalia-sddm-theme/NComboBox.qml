import QtQuick
import QtQuick.Controls
import QtQuick.Layouts

import "./"
import "../Commons/"

RowLayout {
    id: root

    property real minimumWidth: 200 * Style.uiScaleRatio
    property real popupHeight: 180 * Style.uiScaleRatio

    property string textRole: "name"
    property var model
    property string currentKey: ""
    property string placeholder: ""

    property bool showIcon: false
    property string iconName: ""

    property color borderColor: Color.mOutline
    property color borderFocusColor: Color.mSecondary

    property alias currentIndex: combo.currentIndex
    signal activated(int index)
    signal selected(string key)
    signal popupClosed

    property real preferredHeight: Style.baseWidgetSize * 1.1 * Style.uiScaleRatio

    spacing: Style.marginL
    Layout.fillWidth: true

    function itemCount() {
        if (!root.model)
            return 0;
        if (typeof root.model.count === 'number')
            return root.model.count;
        if (Array.isArray(root.model))
            return root.model.length;
        return 0;
    }

    function getItem(index) {
        if (!root.model)
            return null;
        if (typeof root.model.get === 'function')
            return root.model.get(index);
        if (Array.isArray(root.model))
            return root.model[index];
        return null;
    }

    function findIndexByKey(key) {
        for (var i = 0; i < itemCount(); i++) {
            var item = getItem(i);
            if (item && item.key === key)
                return i;
        }
        return -1;
    }

    ComboBox {
        id: combo

        Layout.minimumWidth: root.minimumWidth
        Layout.fillWidth: true
        Layout.preferredHeight: root.preferredHeight
        model: root.model
        textRole: root.textRole

        currentIndex: findIndexByKey(currentKey)
        onActivated: {
            root.activated(combo.currentIndex);

            var item = getItem(combo.currentIndex);
            if (item && item.key !== undefined)
                root.selected(item.key);
        }

        background: Rectangle {
            implicitWidth: Style.baseWidgetSize * 3.75
            implicitHeight: preferredHeight
            color: Color.mSurface
            border.color: combo.activeFocus ? root.borderFocusColor : root.borderColor
            border.width: Style.borderS
            radius: Style.iRadiusM

            Behavior on border.color {
                ColorAnimation {
                    duration: Style.animationFast
                }
            }
        }

        contentItem: Item {
            implicitHeight: root.preferredHeight

            NIcon {
                id: comboLeadingIcon
                visible: root.showIcon && root.iconName !== ""
                icon: root.iconName
                pointSize: Style.fontSizeL
                color: combo.activeFocus ? Color.mSecondary : Color.mOnSurfaceVariant
                anchors.left: parent.left
                anchors.leftMargin: Style.marginL
                anchors.verticalCenter: parent.verticalCenter

                Behavior on color {
                    ColorAnimation { duration: Style.animationFast }
                }
            }

            NText {
                anchors.left: comboLeadingIcon.visible ? comboLeadingIcon.right : parent.left
                anchors.leftMargin: comboLeadingIcon.visible ? Style.marginM : Style.marginL
                anchors.right: parent.right
                anchors.rightMargin: combo.indicator.width + Style.marginL
                anchors.verticalCenter: parent.verticalCenter
                pointSize: Style.fontSizeM
                verticalAlignment: Text.AlignVCenter
                elide: Text.ElideRight
                color: (combo.currentIndex >= 0) ? Color.mOnSurface : Color.mOnSurfaceVariant
                text: (combo.currentIndex >= 0) ? combo.displayText : root.placeholder
            }
        }

        indicator: NIcon {
            x: combo.width - width - Style.marginM
            y: combo.topPadding + (combo.availableHeight - height) / 2
            icon: "caret-down"
            pointSize: Style.fontSizeL
        }

        popup: Popup {
            y: combo.height
            implicitWidth: combo.width - Style.marginM
            implicitHeight: Math.min(root.popupHeight, contentItem.implicitHeight + Style.marginM * 2)
            padding: Style.marginM

            onClosed: root.popupClosed()

            contentItem: NListView {
                model: combo.popup.visible ? root.model : null
                implicitHeight: contentHeight
                horizontalPolicy: ScrollBar.AlwaysOff
                verticalPolicy: ScrollBar.AsNeeded

                delegate: ItemDelegate {
                    property var parentComboBox: combo
                    property int itemIndex: index
                    width: ListView.view ? ListView.view.width : (parentComboBox ? parentComboBox.width - Style.marginM * 3 : 0)
                    hoverEnabled: true
                    highlighted: ListView.view.currentIndex === itemIndex

                    property bool pendingClick: false

                    function handleSelection() {
                        if (!parentComboBox)
                            return;

                        var rootItem = parentComboBox.parent;

                        parentComboBox.currentIndex = itemIndex;
                        parentComboBox.popup.close();

                        if (rootItem) {
                            rootItem.activated(itemIndex);
                        }

                        var keyToEmit = "";

                        if (typeof key !== "undefined") {
                            keyToEmit = key;
                        } else if (typeof modelData !== "undefined" && modelData.key) {
                            keyToEmit = modelData.key;
                        } else {
                            if (rootItem && typeof rootItem.getItem == 'function') {
                                var item = rootItem.getItem(itemIndex);
                                if (item && item.key != undefined) {
                                    keyToEmit = item.key;
                                }
                            }
                        }

                        if (keyToEmit !== "") {
                            if (rootItem && typeof rootItem.getItem == 'function') {
                                rootItem.selected(keyToEmit);
                            }
                        }
                    }

                    Timer {
                        id: clickRetryTimer
                        interval: 50
                        repeat: false
                        onTriggered: {
                            if (parent.pendingClick && parent.ListView.view && !parent.ListView.view.flicking && !parent.ListView.view.moving) {
                                parent.pendingClick = false;
                                parent.handleSelection(); // Call helper
                            } else if (parent.pendingClick) {
                                restart();
                            }
                        }
                    }

                    onHoveredChanged: {
                        if (hovered) {
                            ListView.view.currentIndex = itemIndex;
                        }
                    }

                    onClicked: {
                        if (ListView.view && (ListView.view.flicking || ListView.view.moving)) {
                            ListView.view.cancelFlick();
                            pendingClick = true;
                            clickRetryTimer.start();
                        } else {
                            handleSelection();
                        }
                    }

                    background: Rectangle {
                        anchors.fill: parent
                        color: highlighted ? Color.mHover : Color.transparent
                        radius: Style.iRadiusS
                        Behavior on color {
                            ColorAnimation {
                                duration: Style.animationFast
                            }
                        }
                    }

                    contentItem: NText {
                        text: (typeof name !== "undefined" ? name : (root.getItem(index) ? root.getItem(index).name : ""))
                        pointSize: Style.fontSizeM
                        color: highlighted ? Color.mOnHover : Color.mOnSurface
                        verticalAlignment: Text.AlignVCenter
                        elide: Text.ElideRight
                        Behavior on color {
                            ColorAnimation {
                                duration: Style.animationFast
                            }
                        }
                    }
                }
            }

            background: Rectangle {
                color: Color.mSurfaceVariant
                border.color: Color.mOutline
                border.width: Style.borderS
                radius: Style.iRadiusM
            }
        }

        Connections {
            target: root
            function onCurrentKeyChanged() {
                combo.currentIndex = root.findIndexByKey(currentKey);
            }
        }
    }
}
